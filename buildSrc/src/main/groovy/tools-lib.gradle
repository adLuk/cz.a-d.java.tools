plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

repositories {
    mavenCentral()

}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
//    toolchain {
//        languageVersion.set(JavaLanguageVersion.of("${jdkCompileVersion}"))
//    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                url = 'https://github.com/adLuk/cz.a-d.java.tools'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'casper'
                        name = 'Lukas Masek'
                        email = 'lukas.masek@a-d.cz'
                    }
                }
                scm {
                    connection = 'https://github.com/adLuk/cz.a-d.java.tools.git'
                    url = 'https://github.com/adLuk/cz.a-d.java.tools'
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            name = "OSSRH"
             url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" // this is the repo for snapshots
//            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/" // deploys to the staging repo in nexus
            credentials {
                // Create system environment variables with your token-username and token-password.
                username = property('ossrh.token.username')
                password = property('ossrh.token.password')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}
